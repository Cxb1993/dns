#! /bin/csh 
#BSUB -o prod.o -e prod.e
#BSUB -q small
#XXXX -q large-dq
#BSUB -J iso12
#BSUB -W 0:720
#BSUB -n 128 -R "span[ptile=128]"

module list

set name = iso12_250_
#set refin=$HOME/dns/prod/benchmark128.inp
set refin=$HOME/dns/prod/prod256.inp

# use restart file from PSI?
set restart = 1

# number of cpus to use for mpirun:
set NCPU = 128
# mesh
set mesh = "1 1 $NCPU 256 256 256"


# name of job file to resubmit  (if resubmit = 1)
set resubmit = 0
set JOB = prod.job
# number of times to resubmit:
set resub = 20
set resub_file = $SRC/{$name}resub




set HDIR=`pwd`
set SRC=$HOME/dns/src

# CPQ or BlueMountain?
set OSTYPE = `uname`
if ($OSTYPE == IRIX64) then
   set MPIRUN = mpirun -np $NCPU
else
   set MPIRUN = prun -n $NCPU
endif




cd $SRC
./gridsetup.py $mesh
make -j 4 dns
    
if (!(-d /scratch/taylorm)) then
   mkdir /scratch/taylorm
endif
\cp -f dns  /scratch/taylorm/dns

cd /scratch/taylorm

if ($restart == 1) then

   # check for resub limit
   set out = `$SRC/lsfcheck.py $resub_file $resub`
   echo 'resublimit = ' $resub 
   echo 'resub value in file: '
   cat $resub_file
   if ($out != SMALLER) then
      echo 'resubmit limit reached.  Exit.'
      exit 1
   endif

   #search HPSS for newest restart file
   set resnameu = `psi ls -t dns/{$name}\*.u | sort | tail -1`
   if ($resnameu =="") then
      echo "Error finding restart file.  Exit"
      exit 1
   else
      set resnamev = `psi ls -t dns/{$name}\*.v | sort | tail -1`
      set resnamew = `psi ls -t dns/{$name}\*.w | sort | tail -1`
      echo "Using restart files: " 
      echo $resnameu
      echo $resnamev
      echo $resnamew
   endif
   set resnameu2 = `basename $resnameu`
   set resnamev2 = `basename $resnamev`
   set resnamew2 = `basename $resnamew`

   # check to see if files are left over from last run: 
   if !(-e $resnameu2) then
      psi get $resnameu
   endif
   if !(-e $resnamev2) then
      psi get $resnamev
   endif
   if !(-e $resnamew2) then
      psi get $resnamew
   endif

   \rm -f restart.*
   \mv -f $resnameu2  restart.u
   \mv -f $resnamev2  restart.v
   \mv -f $resnamew2  restart.w
   if !(-e restart.w) then
      echo "No restart.w file"
      exit 1
   endif
   $MPIRUN ./dns -r -d . $name < $refin 
else
   echo 'Initial run.  writing a 0 to ' $resub_file 
   echo '0' > $resub_file

   #cleanup:  
   \rm -f {$name}\*.sf
   \rm -f {$name}\*.scalars
   \rm -f {$name}\*.spec
   \rm -f {$name}\*.u
   \rm -f {$name}\*.v
   \rm -f {$name}\*.w

   $MPIRUN  ./dns  -d . $name < $refin
endif




#remove empty files before saving to HPSS:
echo 'removing size 0 files'
find . -size 0 -exec rm -f {} \; -print

# NOTE: EOF MUST START IN COLUMN 1, AS BELOW:
psi <<EOF
cd dns
save {$name}\*.sf
save {$name}\*.scalars
save {$name}\*.spec
save {$name}\*.u
save {$name}\*.v
save {$name}\*.w
EOF

if ($resubmit == 1) then
    cd $HDIR
    idbsub -n $NCPU < $JOB
endif


# ssrun -workshop mpirun -np 128 ../src/dns < $refin 
# prof ssrun_output_file















