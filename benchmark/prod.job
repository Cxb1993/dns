#! /bin/csh 
#BSUB -o prod.o -e prod.e
#BSUB -q small
#XXXX -q large-dq
#BSUB -J iso12
#BSUB -G telluride-sh
#BSUB -W 720
#BSUB -n 128 -R "span[ptile=128]"

module list
set HDIR=`pwd`
set SRC=$HOME/dns/src

set name = iso12_250_
#set refin=$HOME/dns/prod/benchmark128.inp
set refin=$HOME/dns/prod/prod256.inp

# use restart file from PSI?
set restart = 1

# number of cpus to use for mpirun:
set NCPU = 128
# mesh
set mesh = "1 1 $NCPU 256 256 256"




# CPQ or BlueMountain?
set OSTYPE = `uname`
if ($OSTYPE == IRIX64) then
   set MPIRUN = "mpirun -np $NCPU"
else
   set MPIRUN = "prun -n $NCPU"
endif


if (!(-d /scratch/taylorm)) then
   mkdir /scratch/taylorm
endif
if (!(-d /scratch/taylorm)) then
   echo "/scratch/taylorm does not exist"
   exit 1
endif

cd /scratch/taylorm
\cp -f $SRC/* .
./gridsetup.py $mesh
make dep
make clean
make -j 4 dns


if ($restart == 1) then

   #search HPSS for newest restart file
   $HOME/dns/getrestart.sh $name
   if (!($status==0)) then
      exit 1
   endif
   $MPIRUN ./dns -t -r -d . $name < $refin 
else
   #cleanup:  
   \rm -f {$name}\*.sf
   \rm -f {$name}\*.scalars
   \rm -f {$name}\*.spec
   \rm -f {$name}\*.u
   \rm -f {$name}\*.v
   \rm -f {$name}\*.w

   $MPIRUN  ./dns -t -d . $name < $refin
endif




#remove empty files before saving to HPSS:
echo 'removing size 0 files'
find . -size 0 -exec rm -f {} \; -print

# NOTE: EOF MUST START IN COLUMN 1, AS BELOW:
psi <<EOF
cd dns
save {$name}\*.sf
save {$name}\*.scalars
save {$name}\*.spec
save {$name}\*.u
save {$name}\*.v
save {$name}\*.w
EOF



# ssrun -workshop mpirun -np 128 ../src/dns < $refin 
# prof ssrun_output_file















